import defs;

Page test_page
{
    TestItem    item;
  
    string[] fnames = {"", "", "", ""};

    init()
    {
        fill_pattern_color = TEST_PAGE_COLOR;
    }

    on_event:button_press()
    {
        if(SELECT_ITEMS_BY_MOUSE) {
            int ret = layout.canvas.handle_mouse(input_pointer_x, input_pointer_y, event_time);
            if (ret == CENTER)
                response.process_hit(ret, event_time);
        }
    }

    on_event:key_press()
    { 
        if (input_key == KEY_Shift_L)
            response.process_hit(0, event_time);
        else if (input_key == KEY_Shift_R)
            response.process_hit(1, event_time);
        else if (input_key == KEY_Return || input_key == ' ')
            response.process_hit(2, event_time);
        else if (!eyetracker.handle_key(input_key, input_modifiers)) 
            handle_special_key(this, input_key, input_modifiers);
    }

    on_event:message()
    {
        if (message_sender == eyetracker)
            handle_eyetracker_message();
        else if (message_sender == control)
            handle_control_message();
    }

    void handle_eyetracker_message()
    {
        if (message_arg == DRIFT_CHECK_DONE)
            start_actual_presentation();
        else if (message_arg == RECALIBRATE)
            cancel_to_recalibrate();
    }

    void handle_control_message()
    {
        response.process_hit(message_arg, event_time);
    }

    bool within_GuiItem(int x, int y, GuiItem item)
    {
        if(x < item.x || y < item.y)
            return false;
        else if(x >= item.x + item.width || y >= item.y + item.height )
            return false;
        return true;
    }

    VerticalLayout layout
    {

        CanvasGadget canvas
        {
            init()
            {
                fill_pattern_color = color:white;
                is_visible = true;
                width = CANVAS_WIDTH;
                height = CANVAS_HEIGHT;
            }

            ImageShape image1
            {
                init()
                {
                    offset_x = IMAGE_WIDTH/2;
                    offset_y = IMAGE_WIDTH/2;

                    width  = IMAGE_WIDTH;
                    height = IMAGE_HEIGHT;
                }
            }

            RectangleShape aoi1
            {
                init()
                {
                    offset_x = IMAGE_WIDTH/2;
                    offset_y = IMAGE_HEIGHT/2;

                    width  = IMAGE_WIDTH;
                    height = IMAGE_HEIGHT;

                    line_pattern_color = rgbi:50/255/0/0;
                    line_width = 4.0;
                }
            }

            time load_images(time t)
            {
                image1.image = stimuli_dir() + "/images/" + fnames[CENTER];
                image1.start(t);

                return image1.expected_start_time;
            }

            void highlight_regions(time t)
            {
                aoi1.start(t);
            }

            int handle_mouse(int x, int y, time t)
            {
                // x and y are relative to ImageGadget
                int rel_x = x - x_abs;
                int rel_y = y - y_abs;
                int ret;

                // ignore mouse presses outside of the canvas
                if (rel_x >= IMAGE_WIDTH  / 2                   &&
                    rel_x <  IMAGE_WIDTH  / 2 + IMAGE_WIDTH     &&
                    rel_y >= IMAGE_HEIGHT / 2                   &&
                    rel_y <  IMAGE_HEIGHT / 2 + IMAGE_HEIGHT
                   ) {
                    ret = CENTER;
                }
                else {
                    ret = -1;
                }


                return ret;
            }
        }

        time start_stimulus(time t)
        {
            reset();

            fnames[CENTER] = item.image_filename;

            time expected_start = canvas.load_images(t);

            if(HIGHLIGHT_REGION_OF_INTEREST)
                canvas.highlight_regions(t);

            // Return stimulus onset time.
            return expected_start;
        } 

        void reset()
        {
            //canvas.full_abort();
            canvas.image1.abort();
            canvas.image1.is_visible = false;
            canvas.aoi1.abort();
            canvas.aoi1.is_visible = false;
        } 
    }

    Timer drift_check_timer
    {
        on_event:expire()
        {
            // Tell eye-tracker which target page to draw on and which
            // object to signal when drift-check is done or aborted.
            eyetracker.target_page = test_page;
            eyetracker.target = test_page;

            int x = layout.canvas.x_abs + CANVAS_WIDTH/2;
            int y = layout.canvas.y_abs + CANVAS_HEIGHT/2;

            eyetracker.start_drift_check(x, y);
        }
    }

    SoundPlayback auditory_stimulus
    {
        SoundFile clip {}

        time play(time t)
        {
            abort();

            device = sound_output_device;
            clip.file = stimuli_dir() + "sounds/" + item.sound_filename;

            start(t);

            return expected_start_time;
        }
    }


    Response response
    {
        // Response data.
        int     value;          // response value: 1=L,2=R,-1=timeout
        int     rt;             // response time [ms]
        bool    recalibrate;    // set, when eye-tracker recalibration required
        int     correct;        // correct response: 0=no, 1=yes, -1=n.a. 

        void clear()
        {
            correct = -1; // correct response: 0=no, 1=yes, -1=n.a.  
            value = -1;
            rt = -9999;
            recalibrate = false;
        }


        void process_hit(int val, time t)
        {
            HitType validity = hit(t);

            // Accept the first hit in the response interval and end the 
            // response interval.
            if (validity == HIT_VALID)
            {
                value = val;
                rt = int(reaction_time(0));             
                correct = val == item.expected_answer;

                ignore_remaining_hits();
                auditory_stimulus.abort();         
                
                stop(t + RECORD_EXTRA_AFTER_RESPONSE);
            }
            else if (validity == HIT_TOO_EARLY || validity == HIT_TOO_LATE)
            {
                // False alarm. Deal with this if desired.
            }
        }

        on_event:finish()
        {
            if (item.record_eye_movements)
                eyetracker.stop_tracking();
            
            done(CONTINUE);
        }
    }

    // Aborts any ongoing activity on this page and signals the initiator
    // of the trial that we're done.
    void done(int msgid)
    {
        // Just in case; abort presentation if still active.
        layout.reset();
        auditory_stimulus.abort();  

        signal_target(msgid);   // tell caller we're done
        target = null;

        control.clear_status();
    }

    void cancel_to_recalibrate()
    {
        response.recalibrate = true;
        done(CANCEL);
    }

    //==========================================================================


    // Performs preparatory work required before using this page.
    void setup(int ntrials=-1)
    {
        test_page_overlay.setup(this);

        // Enable or disable the progress bar.
        test_page_overlay.show_progress_bar(ntrials);   // disabled if < 0

        // Enable button-box buttons.
        control.target = this;
        control.disable_buttons();

        // Enable the valid buttons to work with a three button box //
        //                                                          //
        //                                                          //
        //       0                                          2       //
        //                                                          //
        //                             1                            //
        //                                                          //
        // So the left button (0) will be enabled to send a 0       //
        // to the response object and the right button(2) will      //
        // send a 1, whereas the middle button(1) will be ignored.  //

        control.enable_button(0, 0);
        control.enable_button(2, 1);
        
        if(!SELECT_ITEMS_BY_MOUSE)
            hide_cursor();
    }


    // Performs cleaning up if necessary.
    void cleanup()
    {
        test_page_overlay.cleanup();

        control.disable_buttons();
        if(control.target == this)
            control.target = null;

        if(!SELECT_ITEMS_BY_MOUSE)
            show_cursor();
    }

    void quick_show_and_save_picture(
        Object      caller,
        TestItem    it,
        time        tref,
        int         count = -1,
        string      output_directory = "/pictures",
        dur         allowed_time_to_export = 500ms
        )
    {
        item = it;

        // Show this page (if not yet done) and make tref the trial start time.
        tref = test_window1.show_test_page(this, tref + allowed_time_to_export);

        // Update progress counter (if enabled).
        test_page_overlay.update_progress_bar(count);

        // Remember who to signal when the trial is over.
        target = caller;

        time stimonset = layout.start_stimulus(tref);

        save_picture.output_directory = output_directory;
        save_picture.start(stimonset+allowed_time_to_export); 
    }

    Timer save_picture
    {
        string output_directory;
        on_event:expire()
        {
            string fn = output_directory + string(item.type) + format(item.id,3) + ".png";

            if(!file_exists(output_directory))
                create_dir_tree(output_directory);

            println("Flying through to generate pictures of the screen and save it as:\n\t" + fn);

            if(file_exists(fn))
                println("Picture already exists, overwriting!" + fn);

            test_page.export_as_png(fn);

            done(CONTINUE);
        }
    }

    // Starts a trial.
    void action(Object caller, TestItem it, time tref, int count=-1)
    {
        // Save passed trial control parameters.
        item = it;

        // Show this page (if not yet done) and make tref the trial start time.
        tref = test_window1.show_test_page(this, tref + INTERTRIAL_INTERVAL);

        // Reset response data.
        response.clear();

        // Start eye-tracker drift-check at tref. When the drift check is done 
        // the stimulus will be started (see on_event:message() above). On 
        // drift-check failure or abort we'll exit to do a recalibration.
        //SetupActionOrder(tref);

        if (item.do_drift_check)
            drift_check_timer.start(tref);
        else 
            start_actual_presentation();
     
        // Update progress counter (if enabled).
        test_page_overlay.update_progress_bar(count);

        // Update status line on control window. Cleared in done() above.
        control.set_status(
            string(item.id)             + "  " +
            string(item.sound_filename) + "  " +
            string(item.image_filename)
            );

        // Remember who to signal when the trial is over.
        target = caller;
    }

    void  start_actual_presentation()
    {
        if (item.record_eye_movements) 
            eyetracker.start_tracking();

        time visstimonset = layout.start_stimulus(event_time + DC_VISUAL_DELAY);

        time audstimonset = auditory_stimulus.play(visstimonset);
        time audstimoffset = auditory_stimulus.expected_finish_time;

        // Response window starts at onset of the audio and picture target. 

        if(RESPONSE_DURATION > 0s) // auto paced
            response.start(audstimonset, audstimoffset + RESPONSE_DURATION);
        else // self paced
            response.start(audstimonset, RESPONSE_DURATION);

        // Always measure relative to target onset.
        response.reference_time = audstimonset;
    }

// Remove because no one uses it.
//    bool allow_selection_by_mouse()
//    {
//        return SELECT_ITEMS_BY_MOUSE;
//    }

}
